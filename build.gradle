plugins {
    id 'java-library'
    id 'maven-publish'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// Version strings used for CI
// Note the spaces before and after the equals sign
ext.majorVersion = 5
ext.minorVersion = 0
ext.minecraftVersion = "1.12"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'Sonatype'
        url 'https://oss.sonatype.org/content/groups/public'
    }

    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        name 'nu-langchecker'
        url 'https://raw.githubusercontent.com/Librazy/NyaaUtilsLangChecker/maven-repo'
    }

    maven {
        name 'ess-repo'
        url 'http://repo.ess3.net/content/groups/essentials'
    }

    maven {
        name 'langutils-repo'
        url 'https://raw.github.com/MascusJeoraly/LanguageUtils/mvn-repo/'
    }
}

dependencies {
    api "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
    api 'javax.persistence:persistence-api:1.0'
    api 'org.librazy:NyaaUtilsLangChecker:1.0-SNAPSHOT'
    implementation 'net.milkbowl.vault:VaultAPI:1.6'
    implementation('net.ess3:Essentials:2.13-SNAPSHOT') {
        transitive = false
    }
    implementation('com.meowj:LangUtils:1.9') {
        transitive = false
    }
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.xerial:sqlite-jdbc:3.21.0'
}

compileJava {
    options.compilerArgs += ["-Xplugin:NyaaUtilsLangAnnotationProcessor", "-Xlint:all"]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "cat.nyaa"
            artifactId "nyaacore"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
